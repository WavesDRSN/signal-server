plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version "0.9.4"
}

jar {
	enabled = false
}

group = 'wavesDRSN'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	google()
}

def grpcVersion = '1.58.0' // Совместимая с grpc-spring-boot-starter 2.15.0
def protobufVersion = '3.25.5'

dependencies {
	// gRPC
	implementation 'net.devh:grpc-spring-boot-starter:2.15.0.RELEASE'
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "io.grpc:grpc-netty:${grpcVersion}"
	compileOnly "org.apache.tomcat:annotations-api:6.0.53"

	// Lombok и DevTools
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'

	// Тесты (JUnit 5)
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.grpc:grpc-testing:1.58.0' // Версия должна совпадать с grpc-core
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// БД, миграции, JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	runtimeOnly 'org.postgresql:postgresql'

	// Spring Boot стартер для работы с JPA (Hibernate включен)
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Стартер для валидации (в Spring Boot 3+ используется jakarta.validation)
	implementation 'org.springframework.boot:spring-boot-starter-validation'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
